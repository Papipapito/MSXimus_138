// ============================================================================
// tb_yrw801.v — el motor PCM con la YRW801 REAL: valida el formato 12-bit
// (la onda 0/303 del banco) contra un modelo dorado en python.
// Secuencia identica al test 4 del opl4test.rom pero con la onda elegible.
// ============================================================================
`timescale 1ns/1ps

module tb_yrw801;

reg clk_x1 = 0;
always #6.734 clk_x1 = ~clk_x1;
reg clk_eng = 0;
always #13.333 clk_eng = ~clk_eng;     // _104: 37.5MHz (CLKOUT4 del PLLA)
reg clk_host = 0;
always #9.26 clk_host = ~clk_host;

reg rst_n = 0, eng_rst_n = 0;

reg iorq_n = 1, rd_n = 1, wr_n = 1, m1_n = 1;
reg [7:0] a = 0, din = 0;
wire wave_rd;
wire [7:0] wave_dout;
wire wave_wait_n;
wire [1:0] wave_status;
wire signed [15:0] pcm_l, pcm_r;

wire        mem_req, mem_we;
wire [21:0] mem_addr;
wire [7:0]  mem_wdata;
reg  [7:0]  mem_rdata = 0;
reg  [15:0]  mem_rword = 0;   // _104: palabra (cache de palabra)
reg         mem_done_t = 0;

// parametros de la nota (plusargs)
reg [15:0] WAVEN = 303;
reg [15:0] FNUM = 0;
reg [7:0]  OCT = 1;
integer NSAMP = 600;
integer HDRREL = 0;
integer FMT8 = 0;
integer HDR7 = 0;
integer PANV = 0;
// seno de 8 bits, periodo 100 (tabla generada: round(120*sin(2*pi*i/100)))
function [7:0] sin8(input integer i);
    integer v;
    begin
        v = $rtoi(120.0 * $sin(6.28318530718 * i / 100.0));
        sin8 = v[7:0];
    end
endfunction

opl4_pcm dut (
    .rst_n(rst_n), .clk_host(clk_host),
    .iorq_n(iorq_n), .rd_n(rd_n), .wr_n(wr_n), .m1_n(m1_n),
    .addr(a), .din(din),
    .wave_rd(wave_rd), .wave_dout(wave_dout), .wave_wait_n(wave_wait_n),
    .wave_status(wave_status),
    .pcm_l(pcm_l), .pcm_r(pcm_r),
    .clk_eng(clk_eng), .eng_rst_n(eng_rst_n),
    .mem_req(mem_req), .mem_we(mem_we), .mem_addr(mem_addr),
    .mem_wdata(mem_wdata), .mem_rdata(mem_rdata), .mem_rword(mem_rword), .mem_done_t(mem_done_t)
);

// memoria con la YRW801 real + latencia DDR3
reg [7:0] wavemem [0:4194303];
reg        p_pend = 0, p_we;
reg [21:0] p_addr;
reg [7:0]  p_dat;
reg [5:0]  p_cnt, p_lat;
integer    li;
reg mem_req_d = 0;
always @(posedge clk_x1) begin
    mem_req_d <= mem_req;
    if (mem_req && !mem_req_d) begin
        p_pend <= 1; p_we <= mem_we; p_addr <= mem_addr; p_dat <= mem_wdata;
        p_cnt <= 0; p_lat <= 6'd20 + ({$random} % 5);
    end
    else if (p_pend) begin
        p_cnt <= p_cnt + 1;
        if (p_cnt == p_lat) begin
            if (p_we) wavemem[p_addr] <= p_dat;
            else begin
                mem_rdata <= wavemem[p_addr];
                mem_rword[7:0]  <= wavemem[{p_addr[21:1], 1'b0}];
                mem_rword[15:8] <= wavemem[{p_addr[21:1], 1'b1}];
            end
            mem_done_t <= ~mem_done_t;
            p_pend <= 0;
        end
    end
end

task outp(input [7:0] p, input [7:0] v);
begin
    @(negedge clk_host); a = p; din = v; iorq_n = 0; wr_n = 0;
    #420; @(negedge clk_host); wr_n = 1; iorq_n = 1;
    #2500;
end
endtask

reg [7:0] rdv;
task inp(input [7:0] p);
begin
    @(negedge clk_host); a = p; iorq_n = 0; rd_n = 0;
    #460;
    while (!wave_wait_n) @(posedge clk_host);
    #5; rdv = wave_dout;
    #80; @(negedge clk_host); rd_n = 1; iorq_n = 1;
    #1500;
end
endtask

task wreg(input [7:0] r, input [7:0] v);
begin outp(8'h7E, r); outp(8'h7F, v); end
endtask

// volcado de muestras
integer fd, n = 0, i;
reg pcm_seen = 0;
always @(posedge clk_host) begin
    pcm_seen <= dut.pcm_h3;
    if (dut.pcm_h3 !== pcm_seen && n < NSAMP) begin
        $fdisplay(fd, "%0d", pcm_l);
        n = n + 1;
        if (n == NSAMP) begin
            $display("VOLCADAS %0d muestras", n);
            $finish;
        end
    end
end

initial begin
    if (!$value$plusargs("wave=%d", WAVEN)) WAVEN = 303;
    if (!$value$plusargs("fnum=%d", FNUM)) FNUM = 0;
    if (!$value$plusargs("oct=%d", OCT)) OCT = 1;
    if (!$value$plusargs("nsamp=%d", NSAMP)) NSAMP = 600;
    if (!$value$plusargs("hdrrel=%d", HDRREL)) HDRREL = 0;
    if (!$value$plusargs("fmt8=%d", FMT8)) FMT8 = 0;
    if (!$value$plusargs("hdr7=%d", HDR7)) HDR7 = 0;
    if (!$value$plusargs("pan=%d", PANV)) PANV = 0;
    fd = $fopen("pcm_dump.txt", "w");
    $readmemh("yrw801_2m.hex", wavemem);
    if (HDRREL) begin
        // _106: cabecera de la onda 303 COPIADA a la tabla relocada de RAM
        // (wavetblhdr=4 -> base 4*0x80000 = 0x200000; onda 384 = indice 0)
        for (li = 0; li < 12; li = li + 1)
            wavemem[22'h200000 + li] = wavemem[303*12 + li];
    end
    if (FMT8) begin
        // _107-test: onda 8-BIT sintetica (seno periodo 100) en RAM, cabecera
        // relocada fmt=0, start=0x210000, loop=0, end=4000 (raw = ~end)
        wavemem[22'h200000+0]  = 8'h21;  // fmt0 | start[21:16]
        wavemem[22'h200000+1]  = 8'h00;
        wavemem[22'h200000+2]  = 8'h00;
        wavemem[22'h200000+3]  = 8'h00; wavemem[22'h200000+4] = 8'h00;   // loop=0
        wavemem[22'h200000+5]  = 8'hF0; wavemem[22'h200000+6] = 8'h60;   // -4000 (compl. a 2: canon)
        wavemem[22'h200000+7]  = HDR7[7:0];  // {LFO[5:3],VIB[2:0]} (_113: +hdr7=N)
        wavemem[22'h200000+8]  = 8'hF0;  // AR=15 D1R=0
        wavemem[22'h200000+9]  = 8'h00;  // DL=0 D2R=0
        wavemem[22'h200000+10] = 8'h0F;  // RC=0 RR=15
        wavemem[22'h200000+11] = 8'h00;
        for (li = 0; li < 5000; li = li + 1)
            wavemem[22'h210000 + li] = sin8(li % 100);
    end
    #500  rst_n = 1;
    #1000 eng_rst_n = 1;
    #60000;   // dejar acabar el BARRIDO de reset del motor (768 CE ~ 23us):
              // durante RST=1 las RAMs FNUM/LEVEL/PAN se estan barriendo a 0
              // y las escrituras CPU se pierden (en HW nunca pasa: el reset
              // se suelta ~7s antes de que el software escriba nada)
    outp(8'hC6, 8'h05);
    outp(8'hC7, 8'h03);                       // NEW/NEW2
    if (HDRREL || FMT8) wreg(8'h02, 8'h10);   // wavetblhdr=4 (bits 4:2)
    wreg(8'h20, (FNUM[6:0]<<1) | WAVEN[8]);   // FNUM low / WTN8
    wreg(8'h38, (OCT[3:0]<<4) | FNUM[9:7]);   // _104c: FNUM[9:7] en bits 2:0 (canon)
    wreg(8'h50, 8'h01);                       // TL=0, LD
    wreg(8'h08, WAVEN[7:0]);                  // dispara header load
    i = 0;
    inp(8'hC4); inp(8'h7E);
    while (rdv[1] && i < 200) begin
        #10000; inp(8'hC4); inp(8'h7E); i = i + 1;
    end
    $display("LD limpio tras %0d polls", i);
    wreg(8'h68, 8'h80 | PANV[3:0]);           // KEY on (+pan=N, _116)
end

initial begin
    #400000000;  // 400ms guardia (_113: cruzar wraps de loop)
    $display("TIMEOUT con %0d muestras", n);
    $finish;
end

endmodule
